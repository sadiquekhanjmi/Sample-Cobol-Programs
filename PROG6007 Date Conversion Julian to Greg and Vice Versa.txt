      ******************************************************************
      * Author: Mohd S Khan
      * Date:31/07/2025
      * Purpose:Cobol program to convert the Gregorian date to Julian
      * Date and Julian Date to Gregorian date based on the flag value
      * in input sequentail date file and corresponding date. For Julian
      * Flag is 'J' for Gregorian Flag is 'G'. Create an output date
      * file to show the input date type and its value and converted
      * date type with its Value. Also display in Spool date Flag, date
      * type and date value.
      * Tectonics: cobc
      ******************************************************************
      *Only Mandatory Division in COBOL Program.
       IDENTIFICATION DIVISION.

      *Every COBOL Program has a program-id that signifies the Prog name
       PROGRAM-ID. PROG6007.

      *Environment Division signifies the files used as input and output
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
      *****************************************************************
      * INPUT-FILE                                                    *
      *****************************************************************
      *SELECT defines the file name in the program as a logical name,
      *ASSIGN shows the file name in the environment as physical file
      *Organization shows the way the data is organized in the file
      *****************************************************************
      *                                                               *
            SELECT INFILE ASSIGN TO DATEIN
            ORGANIZATION IS SEQUENTIAL
            FILE STATUS IS INFILE-STATUS.

      *****************************************************************
      * OUTPUT-FILE                                                   *
      *****************************************************************
      *SELECT defines the file name in the program as a logical name,
      *ASSIGN shows the file name in the environment as physical file
      *Organization shows the way the data is organized in the file
      *****************************************************************
      *                                                               *
            SELECT OUTFILE ASSIGN TO DATEOUT
            ORGANIZATION IS SEQUENTIAL
            FILE STATUS IS OUTFILE-STATUS.
      *                                                               *
       DATA DIVISION.
       FILE SECTION.
      ****************************************************************
      *FILE DESCRIPTOR FOR INPUT FILE
      ****************************************************************
      *                                                               *
       FD INFILE
           RECORD 80
           BLOCK CONTAINS 0 RECORDS
           DATA RECORD RECINFILE.
      *                                                               *
       01 RECINFILE                        PIC X(80).
      *                                                               *
      ****************************************************************
      *FILE DESCRIPTOR FOR OUTPUT FILE
      ****************************************************************
       FD OUTFILE
           RECORD 80
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD OMITTED.
      *                                                               *
       01 RECOUTFILE                       PIC X(80).
      *                                                               *
       WORKING-STORAGE SECTION.
      ****************************************************************
      *              WORKING VARIABLES                               *
      ****************************************************************
      *Input file Layout
       01 INPUT-RECORD.
          05 DATE-FLAG                     PIC X(01).
             88 JUL                        VALUE 'J'.
             88 GREG                       VALUE 'G'.
          05 FILLER                        PIC X(01) VALUE SPACES.
          05 DATE-VALUE                    PIC 9(08).

      *Output file Layout
       01 OUTPUT-RECORD.
          05 DATE-TYPE-IN                  PIC X(20).
          05 FILLER                        PIC X(01) VALUE ':'.
          05 DATE-VALUE-IN                 PIC X(08).
          05 FILLER                        PIC X(04).
          05 DATE-TYPE-OUT                 PIC X(20).
          05 FILLER                        PIC X(01) VALUE ':'.
          05 DATE-VALUE-OUT                PIC X(08).

      *Temp Variables used in Program
       01 WS-WORK-AREA.
          05 WS-GREG-DATE                  PIC  9(08).
          05 WS-JUL-DATE                   PIC  9(07).
          05 WS-INT-DATE                   PIC  9(07).
      ****************************************************************
      *File Status of the files used in Programs                     *
      ****************************************************************
       01 INFILE-STATUS                    PIC XX   VALUE SPACES.
          88 INFILE-OK                              VALUE '00'.
          88 INFILE-EOF                             VALUE '10'.
       01 OUTFILE-STATUS                   PIC XX   VALUE SPACES.
          88 OUTFILE-OK                             VALUE '00'.
          88 OUTFILE-EOF                            VALUE '10'.
      ****************************************************************
      *Program Name and Abend Return Code in case of Program failure *
      ****************************************************************
       01 STAT.
          05 STAT-PROGRAM                  PIC X(8)   VALUE 'PROG6007'.
          05 STAT-ABEND-CODE               PIC S99    VALUE +16.
      ****************************************************************
      *Record count for Input and Output file used in Program        *
      ****************************************************************
       01 INFILE-TOT                       PIC 9(9) VALUE ZEROES.
       01 OUTFILE-TOT                      PIC 9(9) VALUE ZEROES.
      ****************************************************************
      * Procedure Division where Actual Logic is Implemented         *
      ****************************************************************
       PROCEDURE DIVISION.
      ****************************************************************
      * Main Program Section                                         *
      ****************************************************************
       0000-MAINLINE SECTION.
      *
           PERFORM 1000-START
              THRU 1000-START-EXIT
      *
           PERFORM 2000-PROCESS
              THRU 2000-PROCESS-EXIT
              UNTIL INFILE-EOF
      *
           PERFORM 8000-WRAP
              THRU 8000-WRAP-EXIT.
      *
           STOP RUN.
      *
       0000-MAINLINE-EXIT.
           EXIT.
      ****************************************************************
      *In This Section Open the Files and Read the Input files       *
      ****************************************************************
       1000-START SECTION.
      *-------------------
      *
           PERFORM 1100-OPEN-FILES
              THRU 1100-OPEN-FILES-EXIT

           PERFORM 1200-READ-INFILE
              THRU 1200-READ-INFILE-EXIT.
      *
       1000-START-EXIT.
           EXIT.
      ****************************************************************
      *Open the Files used in Program                                *
      ****************************************************************
       1100-OPEN-FILES SECTION.
      *------------------------
      *
           OPEN INPUT INFILE
           IF NOT INFILE-OK
              DISPLAY 'ERR OPEN INFILE ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *
           OPEN OUTPUT OUTFILE
           IF NOT OUTFILE-OK
              DISPLAY 'ERR OPEN OUTFILE ' OUTFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF.
      *
       1100-OPEN-FILES-EXIT.
           EXIT.
      ****************************************************************
      *Process the logic                                             *
      ****************************************************************
       2000-PROCESS SECTION.
      *---------------------
      *Initialize the Output record Layout
           INITIALIZE OUTPUT-RECORD

      *Gregorian date YYYYMMDD To Julian date  YYYYDDD
           IF GREG
              COMPUTE WS-INT-DATE = FUNCTION INTEGER-OF-DATE(DATE-VALUE)
              COMPUTE WS-JUL-DATE = FUNCTION DAY-OF-INTEGER(WS-INT-DATE)
              MOVE 'GREGORIAN DATE'        TO DATE-TYPE-IN
              MOVE DATE-VALUE              TO DATE-VALUE-IN
              MOVE 'JULIAN DATE'           TO DATE-TYPE-OUT
              MOVE WS-JUL-DATE             TO DATE-VALUE-OUT(1:7)

              DISPLAY 'DATE-FLAG             : '  DATE-FLAG
              DISPLAY 'GREGORIAN DATE        : '  DATE-VALUE
              DISPLAY 'JULIAN DATE           : '  WS-JUL-DATE
              DISPLAY '------------------------'
           END-IF.

      *Julian Date YYYYDDD To Gregorian Date YYYYMMDD
           IF JUL
              MOVE DATE-VALUE(1:7)         TO WS-JUL-DATE
              COMPUTE WS-INT-DATE =
                      FUNCTION INTEGER-OF-DAY(WS-JUL-DATE)
              COMPUTE WS-GREG-DATE =
                      FUNCTION DATE-OF-INTEGER(WS-INT-DATE)
              MOVE 'JULIAN DATE'           TO DATE-TYPE-IN
              MOVE DATE-VALUE(1:7)         TO DATE-VALUE-IN(1:7)
              MOVE 'GREGORIAN DATE'        TO DATE-TYPE-OUT
              MOVE WS-GREG-DATE            TO DATE-VALUE-OUT

              DISPLAY 'DATE-FLAG             : '  DATE-FLAG
              DISPLAY 'JULIAN DATE           : '  DATE-VALUE(1:7)
              DISPLAY 'GREGORIAN DATE        : '  WS-GREG-DATE
              DISPLAY '------------------------'
           END-IF.

      * Write the Output file record and increase the record count by +1

           WRITE RECOUTFILE FROM OUTPUT-RECORD.
           ADD +1 TO OUTFILE-TOT

      *Read the input file for the Next record
           PERFORM 1200-READ-INFILE
              THRU 1200-READ-INFILE-EXIT.
      *
       2000-PROCESS-EXIT.
           EXIT.

      ****************************************************************
      * Close the File and Stat Section                              *
      ****************************************************************
       8000-WRAP SECTION.
      *Close the input file
           CLOSE INFILE
           IF NOT INFILE-OK
              DISPLAY ' ERROR CLOSE INFILE  - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *Close the Output file
           CLOSE OUTFILE
           IF NOT OUTFILE-OK
              DISPLAY ' ERROR CLOSE OUTFILE - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *
           PERFORM 8500-STAT
              THRU 8500-STAT-EXIT.
      *
       8000-WRAP-EXIT.
           EXIT.
      ****************************************************************
      *Stat Section                                                  *
      ****************************************************************
       8500-STAT SECTION.
      *--------------------------
           DISPLAY '============================================'
           DISPLAY 'PROGRAM END :' STAT-PROGRAM
           DISPLAY 'TOTAL-INPUT-REC :' INFILE-TOT
           DISPLAY 'TOTAL-OUTPUT-REC :' OUTFILE-TOT
           DISPLAY '============================================'.
      *
       8500-STAT-EXIT.
           EXIT.
      ****************************************************************
      * Abend Section                                                *
      ****************************************************************
       9000-ABEND SECTION.
      *-------------------
      *
           PERFORM 8500-STAT
              THRU 8500-STAT-EXIT
      *
           CLOSE INFILE
                 OUTFILE
      *
           MOVE STAT-ABEND-CODE           TO RETURN-CODE
           STOP RUN.
      *
       9000-ABEND-EXIT.
           EXIT.

      ****************************************************************
      *Read the Input File into the Input-Record Layout              *
      ****************************************************************
       1200-READ-INFILE SECTION.
      *-------------------------
      *Initailize the Input Record Layout
           INITIALIZE INPUT-RECORD

      *Read the input file into the record Layout
           READ INFILE INTO INPUT-RECORD
      *
           EVALUATE TRUE
               WHEN INFILE-OK
                    ADD +1                  TO INFILE-TOT
               WHEN INFILE-EOF
                    CONTINUE
               WHEN OTHER
              DISPLAY 'ERROR READ INFILE - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-EVALUATE.
      *
       1200-READ-INFILE-EXIT.
           EXIT.
      *
