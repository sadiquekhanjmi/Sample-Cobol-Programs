      ******************************************************************
      * Author:
      * Date:31/07/2025
      * Purpose:Create a COBOL to read the student record from the input
      * sequential file and process each record to calculate the total
      * marks , average marks and grade. Grade as well as student detail
      * are written to the output file
      * Tectonics: cobc
      ******************************************************************
      *Only Mandatory Division in COBOL Program.
       IDENTIFICATION DIVISION.

      *Every COBOL Program has a program-id that signifies the Prog name
       PROGRAM-ID. PROG6005.

      *Environment Division signifies the files used as input and output

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
      *****************************************************************
      * INPUT-FILE                                                    *
      *****************************************************************
      *SELECT defines the file name in the program as a logical name,
      *ASSIGN shows the file name in the environment as physical file
      *Organization shows the way the data is organized in the file
      *****************************************************************
      *                                                               *
            SELECT INFILE ASSIGN TO INPUT1
            ORGANIZATION IS SEQUENTIAL
            FILE STATUS IS INFILE-STATUS.

      *****************************************************************
      * OUTPUT-FILE                                                   *
      *****************************************************************
      *SELECT defines the file name in the program as a logical name,
      *ASSIGN shows the file name in the environment as physical file
      *Organization shows the way the data is organized in the file
      *****************************************************************
      *                                                               *
            SELECT OUTFILE ASSIGN TO OUTPUT1
            ORGANIZATION IS SEQUENTIAL
            FILE STATUS IS OUTFILE-STATUS.
      *                                                               *
       DATA DIVISION.

       FILE SECTION.
      ****************************************************************
      *FILE DESCRIPTOR FOR INPUT FILE
      ****************************************************************
      *                                                               *
       FD INFILE
           RECORD 80
           BLOCK CONTAINS 0 RECORDS
           DATA RECORD RECINFILE.
      *                                                               *
       01 RECINFILE                        PIC X(80).
      *                                                               *
      ****************************************************************
      *FILE DESCRIPTOR FOR OUTPUT FILE
      ****************************************************************
       FD OUTFILE
           RECORD 80
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD OMITTED.
      *                                                               *
       01 RECOUTFILE                       PIC X(80).
      *                                                               *
       WORKING-STORAGE SECTION.
      ****************************************************************
      *              WORKING VARIABLES                               *
      ****************************************************************
      *Input file Layout
       01 INPUT-RECORD.
          05 STUDENT-ID                    PIC X(10).

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 STUDENT-NAME                  PIC X(30).

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 STUDENT-MARKS OCCURS 5 TIMES.
             10 SUBJECT-MARK               PIC 9(03).

      *Output file Layout
       01 OUTPUT-RECORD.
          05 STUDENT-ID-OUT                PIC X(10).

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 STUDENT-NAME-OUT              PIC X(30).

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 TOTAL-MARKS                   PIC 9(03).

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 AVERAGE-MARKS                 PIC 9(03).99.

          05 FILLER                        PIC X(01) VALUE SPACES.

          05 GRADE                         PIC X(01).

      *Temp Variables used in Program
       01 SUBJECT-COUNT             PIC 9 VALUE 5.

       01 MAX-MARK                  PIC 9(3) VALUE 100.

       01 MIN-PASS-MARK             PIC 9(3) VALUE 040.

       88 FIRST-MARK                VALUE IS 060 THRU 100.

       88 SECOND-MARK               VALUE IS 050 THRU 059.

       88 THIRD-MARK                VALUE IS 040 THRU 049.

       01 FIRST-GRADE               PIC X VALUE 'A'.

       01 SECOND-GRADE              PIC X VALUE 'B'.

       01 THIRD-GRADE               PIC X VALUE 'C'.

       01 FAILING-GRADE             PIC X VALUE 'F'.

       01 WS-I                      PIC 9(3).

      ****************************************************************
      *File Status of the files used in Programs                     *
      ****************************************************************
       01 INFILE-STATUS                    PIC XX   VALUE SPACES.
          88 INFILE-OK                              VALUE '00'.
          88 INFILE-EOF                             VALUE '10'.
       01 OUTFILE-STATUS                   PIC XX   VALUE SPACES.
          88 OUTFILE-OK                             VALUE '00'.
          88 OUTFILE-EOF                            VALUE '10'.
      ****************************************************************
      *Program Name and Abend Return Code in case of Program failure *
      ****************************************************************
       01 STAT.
          05 STAT-PROGRAM                  PIC X(8)   VALUE 'PROG6005'.
          05 STAT-ABEND-CODE               PIC S99    VALUE +16.
      ****************************************************************
      *Record count for Input and Output file used in Program        *
      ****************************************************************
       01 INFILE-TOT                       PIC 9(9) VALUE ZEROES.
       01 OUTFILE-TOT                      PIC 9(9) VALUE ZEROES.
      ****************************************************************
      * Procedure Division where Actual Logic is Implemented         *
      ****************************************************************
       PROCEDURE DIVISION.
      ****************************************************************
      * Main Program Section                                         *
      ****************************************************************
       0000-MAINLINE SECTION.
      *
           PERFORM 1000-START
              THRU 1000-START-EXIT
      *
           PERFORM 2000-PROCESS
              THRU 2000-PROCESS-EXIT
              UNTIL INFILE-EOF
      *
           PERFORM 8000-WRAP
              THRU 8000-WRAP-EXIT.
      *
           STOP RUN.
      *
       0000-MAINLINE-EXIT.
           EXIT.

      ****************************************************************
      *In This Section Open the Files and Read the Input files       *
      ****************************************************************

       1000-START SECTION.
      *-------------------
      *
           PERFORM 1100-OPEN-FILES
              THRU 1100-OPEN-FILES-EXIT

           PERFORM 1200-READ-INFILE
              THRU 1200-READ-INFILE-EXIT.
      *
       1000-START-EXIT.
           EXIT.
      ****************************************************************
      *Open the Files used in Program                                *
      ****************************************************************
       1100-OPEN-FILES SECTION.
      *------------------------
      *
           OPEN INPUT INFILE

           IF NOT INFILE-OK
              DISPLAY 'ERR OPEN INFILE ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *
           OPEN OUTPUT OUTFILE

           IF NOT OUTFILE-OK
              DISPLAY 'ERR OPEN OUTFILE ' OUTFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF.
      *
       1100-OPEN-FILES-EXIT.
           EXIT.

      ****************************************************************
      *Process the logic                                             *
      ****************************************************************

       2000-PROCESS SECTION.

      *---------------------
      *Initialize the Output record Layout

           INITIALIZE OUTPUT-RECORD

      *Add the all Subject Marks to get the Total Marks

           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > SUBJECT-COUNT
               ADD SUBJECT-MARK(WS-I) TO TOTAL-MARKS
           END-PERFORM

      *Calculate the Average Marks

           COMPUTE AVERAGE-MARKS = TOTAL-MARKS / SUBJECT-COUNT

      *Calulate the Grade

           IF AVERAGE-MARKS < MIN-PASS-MARK

              MOVE FAILING-GRADE TO GRADE

           ELSE

              IF AVERAGE-MARKS >= 060 AND AVERAGE-MARKS <= 100

                 MOVE FIRST-GRADE TO GRADE

              ELSE

                 IF AVERAGE-MARKS >= 050 AND AVERAGE-MARKS < 060

                    MOVE SECOND-GRADE TO GRADE

                 ELSE

                    IF AVERAGE-MARKS >= 040 AND AVERAGE-MARKS < 049

                       MOVE THIRD-GRADE TO GRADE

                    END-IF

                 END-IF

              END-IF

           END-IF

      *Move the data from input to output file

           MOVE STUDENT-ID TO STUDENT-ID-OUT
           MOVE STUDENT-NAME TO STUDENT-NAME-OUT

      * Write the Output file record and increase the record count by +1

           WRITE RECOUTFILE FROM OUTPUT-RECORD.
           ADD +1 TO OUTFILE-TOT

           PERFORM 1200-READ-INFILE
              THRU 1200-READ-INFILE-EXIT.
      *
       2000-PROCESS-EXIT.
           EXIT.

      ****************************************************************
      * Close the File and Stat Section                              *
      ****************************************************************
       8000-WRAP SECTION.
      *
           CLOSE INFILE

           IF NOT INFILE-OK
              DISPLAY ' ERROR CLOSE INFILE  - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *
           CLOSE OUTFILE

           IF NOT OUTFILE-OK
              DISPLAY ' ERROR CLOSE OUTFILE - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-IF
      *
           PERFORM 8500-STAT
              THRU 8500-STAT-EXIT.
      *
       8000-WRAP-EXIT.
           EXIT.

      ****************************************************************
      *Stat Section                                                  *
      ****************************************************************

       8500-STAT SECTION.
      *--------------------------
           DISPLAY '============================================'
           DISPLAY 'PROGRAM END :' STAT-PROGRAM
           DISPLAY 'TOTAL-INPUT-REC :' INFILE-TOT
           DISPLAY 'TOTAL-OUTPUT-REC :' OUTFILE-TOT
           DISPLAY '============================================'.
      *
       8500-STAT-EXIT.
           EXIT.

      ****************************************************************
      * Abend Section                                                *
      ****************************************************************

       9000-ABEND SECTION.
      *-------------------
      *
           PERFORM 8500-STAT
              THRU 8500-STAT-EXIT
      *
           CLOSE INFILE
                 OUTFILE
      *
           MOVE STAT-ABEND-CODE           TO RETURN-CODE
           STOP RUN.
      *
       9000-ABEND-EXIT.
           EXIT.

      ****************************************************************
      *Read the Input File into the Input-Record Layout              *
      ****************************************************************

       1200-READ-INFILE SECTION.
      *-------------------------
      *
           INITIALIZE INPUT-RECORD

           READ INFILE INTO INPUT-RECORD
      *
           EVALUATE TRUE
               WHEN INFILE-OK
                    ADD +1                  TO INFILE-TOT
               WHEN INFILE-EOF
                    CONTINUE
               WHEN OTHER
              DISPLAY 'ERROR READ INFILE - ' INFILE-STATUS
              PERFORM 9000-ABEND
                 THRU 9000-ABEND-EXIT
           END-EVALUATE.
      *
       1200-READ-INFILE-EXIT.
           EXIT.
      *
